plugins {
    id 'com.gradleup.shadow' version '9.0.0-beta4'
    id 'java'
}

group = 'org.ecorte'
version = '1.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-pre3-R0.1-SNAPSHOT")
    implementation("dev.jorel:commandapi-paper-shade:11.0.1-SNAPSHOT")
    compileOnly("net.luckperms:api:5.4")
    implementation("org.bstats:bstats-bukkit:3.0.2")
    implementation("com.zaxxer:HikariCP:4.0.3")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.withType(ShadowJar).configureEach {
    relocate 'com.zaxxer.hikari', 'org.ecorte.shaded.hikari'
    relocate 'org.bstats', 'org.ecorte.shaded.bstats'
    relocate 'dev.jorel.commandapi', 'org.ecorte.shaded.commandapi'
    minimize()
}

jar {
    enabled = false
}

shadowJar {
    archiveClassifier.set('')
}
